apply from: '../artifactConfig.gradle'
apply plugin: 'maven-publish'

afterEvaluate {
    publishing {

        publications {
            bar(MavenPublication) {
                groupId = "$artifactConfig.group"
                artifactId = "$project.artifactId"
                version = "$artifactConfig.version"

                if (project.plugins.hasPlugin("com.android.library")) {
                    from components.release

                    pom.withXml {
                        Node pomNode = asNode()
                        pomNode.dependencies.'*'.findAll() {
                            it.groupId.text() == artifactConfig.group
                        }.each() { it.parent().remove(it) }

                        pomNode.dependencies.'*'.findAll() {
                            it.scope.text() == 'runtime'
                        }.each { it.scope*.value = 'compile' }
                    }
                } else {
                    from components.java
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/tangem/tangem-hot-sdk-kotlin")
                credentials {
                    username = "$githubUser"
                    password = "$githubPass"
                }
            }
        }
    }
}
